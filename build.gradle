plugins {
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.134' apply false
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'net.neoforged.moddev'
    java.toolchain.languageVersion = JavaLanguageVersion.of(21)

    neoForge {
        version = project.neo_version
        parchment {
            mappingsVersion = project.parchment_mappings_version
            minecraftVersion = project.parchment_minecraft_version
        }
    }

    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
        manifest {
            attributes([
                    'Specification-Title'     : mod_name,
                    'Specification-Vendor'    : mod_author,
                    'Specification-Version'   : project.version,
                    'Implementation-Title'    : project.name,
                    'Implementation-Version'  : project.version,
                    'Implementation-Vendor'   : mod_author,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestamp'               : System.currentTimeMillis(),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Built-On-Minecraft'      : minecraft_version
            ])
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'BlameJared'
            url = 'https://maven.blamejared.com'
        }
        maven {
            url = "https://maven.neoforged.net/releases"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release.set(21)
    }

    processResources {
        def expandProps = [
                "version": project.version,
                "mod_id": mod_id,
                "mod_name": mod_name,
                "mod_author": mod_author,
                "minecraft_version": minecraft_version,
                "loader_version_range": loader_version_range,
                "mod_credits": mod_credits,
                "mod_description": mod_description,
                "mod_logo_file": mod_logo_file,
                "mod_license": mod_license,
                "mod_version": mod_version,
                "neo_version": neo_version,
                "minecraft_version_range": minecraft_version_range
        ]
        filesMatching(['pack.mcmeta', 'META-INF/neoforge.mods.toml', '*.mixins.json']) {
            expand expandProps
        }
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }
}