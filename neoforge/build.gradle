plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
}

evaluationDependsOn(':common')

version = mod_version
group = mod_group_id

base {
    archivesName = "${mod_id}-neoforge-${minecraft_version}"
}

neoForge {
    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }
        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
            sourceSet(project(":common").sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

repositories {

}

dependencies {
    implementation project(":common")
    testImplementation project(":common")
}

tasks.named('sourcesJar', Jar) {
    from project(':common').sourceSets.main.allSource
}

// 2. 让 javadoc 任务包含 common 的源码进行解析
tasks.named('javadoc', Javadoc) {
    source project(':common').sourceSets.main.allJava
    // 确保 classpath 包含 common 的依赖，否则无法解析引用
    classpath += project(':common').sourceSets.main.compileClasspath
}

jar {
    from project(":common").sourceSets.main.output
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
